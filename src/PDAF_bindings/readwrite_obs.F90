! $Id: readwrite_obs.F90 82 2019-02-26 15:12:22Z lnerger $
!BOP
!
! !DESCRIPTION:
! This file contains routines for writing and reading
! synthetic observations that are generated by PDAF. 
! For the files the netCDF file format is used. 
! Usually can can use these routines without modifications.
!
! !ROUTINE: init_file_obs --- Initialize observation file
!
! !INTERFACE:
SUBROUTINE init_file_syn_obs(dim_obs_max, file_obs, screen)

! !DESCRIPTION:
! This routine initializes the file holding synthetic 
! observations generated by PDAF.
!
! The routine should only be called by a single filter
! process of only one file is generated. If a separate file
! for each process shoul dbe generated, each process has to
! call this routine.
!
! !REVISION HISTORY:
! 2019-01 - Lars Nerger - Initial code
! Later revisions - see svn log

! !USES:
  IMPLICIT NONE

! !ARGUMENTS:
  INTEGER, INTENT(in) :: dim_obs_max       ! maximum dimension of full observation vector
  CHARACTER(len=*), INTENT(in) :: file_obs ! Name of observation file
  INTEGER, INTENT(in) :: screen            ! Whether to print output
!EOP

  INCLUDE 'netcdf.inc'

! Local variables
  INTEGER :: i, s                               ! Counters
  INTEGER :: dimid_iter, dimid_one, dimid_obs   ! IDs for dimensions
  INTEGER :: id_time, id_obs, id_var            ! IDs for variables
  INTEGER :: dimids(2)                          ! Array of IDs
  INTEGER :: stat(100)                          ! Status array
  INTEGER :: fileid                             ! netcdf file id
  CHARACTER(len=150) :: attstr                  ! String for attribute


  ! Print name file observation file
  IF (screen>0) THEN
     WRITE (*,'(/5x,a,a)') 'GENOBS: Initialize file for synthetic observations: ',TRIM(file_obs)
  END IF

! *** Initialize file
  s = 1
  stat(s) = NF_CREATE(file_obs, 0, fileid) 

  attstr  = 'Synthetic observations'
  s = s + 1
  stat(s) = NF_PUT_ATT_TEXT(fileid, NF_GLOBAL, 'title', LEN_TRIM(attstr), &
       TRIM(attstr)) 

  ! Define dimensions
  s = s + 1
  stat(s) = NF_DEF_DIM(fileid, 'dim_obs_max', dim_obs_max, dimid_obs)
  s = s + 1
  stat(s) = NF_DEF_DIM(fileid, 'one',  1, dimid_one)
  s = s + 1
  stat(s) = NF_DEF_DIM(fileid, 'steps',  NF_UNLIMITED, dimid_iter)

  ! Define variables
  s = s + 1
  stat(s) = NF_DEF_VAR(fileid, 'time', NF_DOUBLE, 1, dimid_iter, Id_time) 

  dimids(1) = DimId_obs
  dimids(2) = dimid_iter

  s = s + 1
  stat(s) = NF_DEF_VAR(fileid, 'obs', NF_DOUBLE, 2, dimids, Id_obs)
  s = s + 1
  stat(s) = NF_DEF_VAR(fileid, 'dim_obs_f', NF_INT, 1, dimids(2), Id_var)
  s = s + 1
  stat(s) = NF_ENDDEF(fileid) 

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in init of observation file, no.', i
  END DO

  ! Close file
  s = s + 1
  stat(s) = nf_close(fileid)

END SUBROUTINE init_file_syn_obs
! ---------------------------------------------------
!BOP
!
! !ROUTINE: write_syn_obs --- write vector of synthetic observations to file
!
! !INTERFACE:
SUBROUTINE write_syn_obs(step, file_obs, dim_obs_f, observation_f, screen)

! !DESCRIPTION:
! This routine writes observations into a netcdf file
!
! The routine should only be called by a single filter
! process if a single file is used (usually holding
! all available observations). If separate files
! for difference processes are used, each of these
! processes needs to call the routine.
!
! !REVISION HISTORY:
! 2019-01 - Lars Nerger - Initial code
! Later revisions - see svn log

! !USES:
  IMPLICIT NONE

! !ARGUMENTS:
  INTEGER, INTENT(in) :: step        ! Currrent time step
  INTEGER, INTENT(in) :: dim_obs_f   ! Dimension of full observation vector
  REAL, INTENT(in)    :: observation_f(dim_obs_f) ! Full observation vector
  CHARACTER(len=*)    :: file_obs    ! Name of observation file
  INTEGER, INTENT(in) :: screen      ! Whether to print output
!EOP

  INCLUDE 'netcdf.inc'

! Local variables
  INTEGER :: i, s                         ! Counters
  INTEGER, save :: iter=0                 ! file iteration counter
  INTEGER :: fileid                       ! netcdf-ID of file
  INTEGER :: id_time, id_obs, id_dimobsf  ! IDs for dimensions
  INTEGER :: id_dimobsmax                 ! ID for dimension variable
  INTEGER :: stat(100)                    ! status flag
  INTEGER :: countv(2), startv(2)         ! index arrays for writing

  INTEGER :: dim_obs_max   ! maximum observation dimension in file
  INTEGER :: dim_obs_store ! size of observation vector to be stored, min(dim_obs_max, dim_obs_f)


  
! ************************************************
! *** Configuration                            ***
! ************************************************

  ! Increment file position counter
  iter = iter + 1

  ! Print name file observation file
  IF (screen>0) THEN
     WRITE (*,'(5x,a,i7,a,a)') 'GENOBS: Write synthetic observations at index ', iter, &
          ' to file: ',TRIM(file_obs)
  END IF

! ***********************************************
! *** Write observational information to file ***
! ***********************************************

  ! Open file
  s = 1
  stat(s) = NF_OPEN(TRIM(file_obs), NF_WRITE, fileid)

  ! Read maximum number of observations
  s = s + 1
  stat(s) = NF_INQ_DIMID(fileid, 'dim_obs_max', id_dimobsmax)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(fileid, id_dimobsmax, dim_obs_max)
  s = s + 1


  ! Write time
  stat(s) = NF_INQ_VARID(fileid, 'time', id_time)
  s = s + 1

  startv(1) = iter
  countv(1) = 1
  stat(s) = NF_PUT_VARA_DOUBLE(fileid, id_time, startv(1), countv(1), REAL(step))
  s = s + 1

  ! Write dim_obs_f
  stat(s) = NF_INQ_VARID(fileid, 'dim_obs_f', id_dimobsf)
  s = s + 1

  IF (dim_obs_f<= dim_obs_max) THEN
     dim_obs_store = dim_obs_f
  ELSE
     dim_obs_store = dim_obs_max
     WRITE (*,*) 'WARNING: Truncating observations to maximum observation dimension in file'
  END IF
  startv(1) = iter
  countv(1) = 1
  stat(s) = NF_PUT_VARA_INT(fileid, id_dimobsf, startv(1), countv(1), dim_obs_store)
  s = s + 1

  ! Write vector of observations
  stat(s) = NF_INQ_VARID(fileid, 'obs', id_obs)
  s = s + 1

  startv(2) = iter
  countv(2) = 1
  startv(1) = 1
  countv(1) = dim_obs_store
  stat(s) = NF_PUT_VARA_DOUBLE(fileid, id_obs, startv, countv, observation_f)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in writing observations, no.', i
     IF (stat(i) /= NF_NOERR) &
          write (*,*)  NF_STRERROR(stat(i))
  END DO

! *****************
! *** Clean up  ***
! *****************

  ! Close files
  s = s + 1
  stat(s) = nf_close(fileid)
  
END SUBROUTINE write_syn_obs
! ---------------------------------------------------
!BOP
!
! !ROUTINE: read_syn_obs --- read vector of synthetic observations from file
!
! !INTERFACE:
SUBROUTINE read_syn_obs(file_obs, dim_obs_f, observation_f, offset, screen)

! !DESCRIPTION:
! This routine writes observations into a netcdf file
!
! The routine should only be called by a single filter
! process if a single file is used (usually holding
! all available observations). If separate files
! for difference processes are used, each of these
! processes needs to call the routine.
!
! !REVISION HISTORY:
! 2019-01 - Lars Nerger - Initial code
! Later revisions - see svn log

! !USES:
  IMPLICIT NONE

! !ARGUMENTS:
!  INTEGER, INTENT(in) :: step        ! Currrent time step
  INTEGER, INTENT(in) :: dim_obs_f   ! Dimension of full observation vector
  REAL, INTENT(out)   :: observation_f(dim_obs_f) ! Full observation vector
  CHARACTER(len=*)    :: file_obs    ! Name of observation file
  INTEGER, INTENT(in) :: offset      ! Offset for observation set index
  INTEGER, INTENT(in) :: screen      ! Whether to print output
!EOP

  INCLUDE 'netcdf.inc'

! Local variables
  INTEGER :: i, s                         ! Counters
  INTEGER, save :: iter=0                 ! file iteration counter
  INTEGER :: fileid                       ! netcdf-ID of file
  INTEGER :: id_obs                       ! IDs for dimensions
  INTEGER :: id_dimobsmax                 ! ID for dimension variable
  INTEGER :: stat(100)                    ! status flag
  INTEGER :: countv(2), startv(2)         ! index arrays for writing

  INTEGER :: dim_obs_max   ! maximum observation dimension in file
  INTEGER :: dim_obs_store ! size of observation vector to be stored, min(dim_obs_max, dim_obs_f)


  
! ************************************************
! *** Configuration                            ***
! ************************************************

  ! Increment file position counter
  iter = iter + 1

  ! Print name file observation file
  IF (screen>0) THEN
     WRITE (*,'(5x,a,i7,a,a)') 'Read synthetic observations at step ', iter+offset, &
          ' from file: ',TRIM(file_obs)
  END IF

! ***********************************************
! *** Write observational information to file ***
! ***********************************************

  ! Open file
  s = 1
  stat(s) = NF_OPEN(TRIM(file_obs), NF_WRITE, fileid)

  ! Read maximum number of observations
  s = s + 1
  stat(s) = NF_INQ_DIMID(fileid, 'dim_obs_max', id_dimobsmax)
  s = s + 1
  stat(s) = NF_INQ_DIMLEN(fileid, id_dimobsmax, dim_obs_max)
  s = s + 1

  IF (dim_obs_f<= dim_obs_max) THEN
     dim_obs_store = dim_obs_f
  ELSE
     dim_obs_store = dim_obs_max
     WRITE (*,*) 'WARNING: Truncating observations to maximum observation dimension in file'
  END IF

  ! Write vector of observations
  stat(s) = NF_INQ_VARID(fileid, 'obs', id_obs)
  s = s + 1

  startv(2) = iter+Offset
  countv(2) = 1
  startv(1) = 1
  countv(1) = dim_obs_store
  stat(s) = NF_GET_VARA_DOUBLE(fileid, id_obs, startv, countv, observation_f)

  DO i = 1,  s
     IF (stat(i) /= NF_NOERR) &
          WRITE(*, *) 'NetCDF error in reading observations, no.', i
     IF (stat(i) /= NF_NOERR) &
          write (*,*)  NF_STRERROR(stat(i))
  END DO

! *****************
! *** Clean up  ***
! *****************

  ! Close files
  s = s + 1
  stat(s) = nf_close(fileid)
  
END SUBROUTINE read_syn_obs
